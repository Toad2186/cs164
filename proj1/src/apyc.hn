/* -*- mode: fundamental; c-file-style: "stroustrup"; indent-tabs-mode: nil -*- */

/* apyc: Bison Parser */

/* Authors:
 * Toan Vuong
 * Eugene Huang
 * Dennis Rong
*/

/*
 * LIST OF CONFLICTS
 * A list of conflicts encountered that we solved using GLR. The compiler will
 * still complain, but these conflicts should be ignored. Other conflicts
 * should be added to this list if solved with GLR. Otherwise fix it
 * so the compiler doesn't complain anymore!
 * -4x conflicts in target r/r
 * -1x expression_list_no_dangling s/r


*/


%define semantic_tree_type "AST"

%{
  #include <string>
  #include <algorithm>
  #include <stack>
  #include "apyc.h"
  #include "ast.h"
  #include <stdlib.h>

  using namespace std;

  static AST_Ptr theTree;

/* Utilities */
//-----------------------------------------------------------------------------
//  Generalizable make functions
//  NOTE: Most of these were defined before we realized that we can
//        easily handle them with horn syntax.
//        If we have time, we can go back and remove most of these,
//        and replace with inline horn syntax.
//-----------------------------------------------------------------------------

/*
 * Given a list of operands and OP, make a tree:
 * (OP, N, Expr(0), Id(1), Expr(2))
 * Where Expr(i) is the direct node from the list of operands
 * and Id(1) is the node returned by make_id of the first operand
*/
static AST_Ptr make_expr_id_expr(const List_Ptr &operands, int op); 

/*
 * Given a list of operands and OP, make a tree:
 * (OP, N, Expr(0), Expr(1))
 * Where Expr(i) is the direct node from the list of operands
*/
static AST_Ptr make_expr_expr(const List_Ptr &operands, int op);

//-----------------------------------------------------------------------------
//  Specific make functions that may use those above internally.
//  At least one of these per entry in the AST table in proj1 handout
//-----------------------------------------------------------------------------

/** Return a new unop node corresponding to OPERANDS, which is the list of
*  right-hand-side semantic values of a unop production (consisting of a
*  token for the operator, followed by the AST for the operand). */
static AST_Ptr make_unop (const List_Ptr& operands);

/** Return a new binop node corresponding to OPERANDS, which is the list of
*  right-hand-side semantic values of a binop production (consisting of an
*  AST for the first operand, a token for the operator, and an AST for the
*  second operand). */
static AST_Ptr make_binop(const List_Ptr &operands);

/*
 * Makes a left_compare node
*/
static AST_Ptr make_left_compare(const List_Ptr &operands);

/*
 * Makes  acompare node
*/
static AST_Ptr make_compare(const List_Ptr &operands);

/*
* Makes an id from the first operand in OPERANDS.
*/
static AST_Ptr make_id (const List_Ptr& operands);

/** Return a new ID node, where the text of the identifier is given by
*  TOKEN. */
static AST_Ptr make_id (AST_Ptr& token);

/*
 * Makes an id token from text
*/
static AST_Ptr make_id (const char *text, const char*loc);

/*
 * Makes an if_expr node.
 * Expects a list of three operands: condition, expr1, expr2
*/
static AST_Ptr make_if_expr(const List_Ptr &operands);

/*
 * Makes an and node.
 * Expects a list of two operands: expr1 and expr2
*/
static AST_Ptr make_and(const List_Ptr &operands);

/*
 * Makes an or node.
 * Expects a list of two operands: expr1 and expr2
*/
static AST_Ptr make_or(const List_Ptr &operands);

/*
 * Makes a not node.
 * Expects a list of one operand: expr1
*/
static AST_Ptr make_not(const List_Ptr &operands);

/*
 * Tuples are taken care of with horn syntax. No
 * function defined.
*/

/*
 * Dicts are taken care of with horn syntax. No
 * function defined.
*/

/*
 * Lists are taken care of with horn syntax. No
 * function defined.
*/

/*
 * TODO: See the "literal" rule
 * String literals
*/

/*
 * TODO: Function calls
 * Not sure how these work
*/

/*
 * Makes an assignment
*/
static AST_Ptr make_assign(const List_Ptr &operands);

//-----------------------------------------------------------------------------
//  Validation functions
//-----------------------------------------------------------------------------
// Keeps track if we're in an if
static int if_counter;
// Keeps track if we're in a loop, and if so, how many
static int loop_counter;
// Keeps track if we're in a function
static int func_counter;
// Are we in a class?
static bool inside_class;
/*
 * Makes sure class is not nested inside a class
*/
static void validate_class();
/*
 * Makes sure return is inside a function
*/
static void validate_return();
/*
 * Makes sure break is valid (inside a loop).
*/
static void validate_break();
/*
 * Makes sure continue is valid (inside a loop).
*/
static void validate_continue();
//-----------------------------------------------------------------------------
// HELPER FUNCTIONS 
//-----------------------------------------------------------------------------
static string invalid_keywords[15] = {"as", "assert", "del", "except", "exec", "finally", "from", "future", "global", "import", "lambda", "raise", "try", "with", "yield"};
static string keywords[18] = {"and", "break", "class", "continue", "def", "elif", "else", "for", "from", "if", "in", "is", "not", "or", "pass", "print", "return", "while"};

static stack<int> indent_stack;
static int current_indent_length = 0;
static void check_identifier(const char *text);
static int indent_size(const char *indent);

static int indent(const char *indent);

%}

/* Precedence and associativity rules */
%left "or"
%left "and"
%left "not"
%left "<" ">" "==" ">=" "<=" "!=" "is" "in"
%left "+" "-"
%left "%" "*" "/" "//"
%left UNOP
%right "**"

%debug
%glr-parser
%define error_function error

/* A token declaration such as
 *   %token INDENT
 * declares a new integer constant INDENT, which serves as a syntactic category
 * (suitable for output by the lexer) identifying a kind of token, which we
 * also often refer to as INDENT. A declaration such as
 *   %token IF "if"
 * defines a consant IF and makes it the syntactic category for the token
 * "if".  The built-in function yyexternal_token_name(...) maps between 
 * syntactic categories (like IF) and the token strings defined in these 
 * %token declarations (like "if").  
 *
 * The Horn system uses tokens as leaf nodes and as AST node operators.  A
 * rule such as
 *     if_stmt: "if"^ expression ":"! ...
 * with no action, by default creates an AST node whose operator is an "if"
 * token (with syntactic category IF, given the %token declaration above).
 * When the node type does not correspond to a token that appears in the 
 * program, we define a dummy token type that serves only as an AST
 * node type.  For example, we can define
 *     %token DICT_DISPLAY ...
 * and then use
 *     dict_display: "{"! key_datum_list? "}"! 
 *              { $$ = $^(DICT_DISPLAY, $*); }
 * in the grammar (using DICT_DISPLAY only in actions, not as a grammar
 * symbol).
 *
 * In this skeleton, we've used a simple kludge to extract the printed forms
 * of AST operators from the operators themselves.  When a token such as IF
 * is the operator, yyexternal_token_name returns the operator name directly.
 * When there is no such correspondence, as with DICT_DISPLAY, we'll define
 *     %token DICT_DISPLAY "@dict_display"
 * Since "@dict_display" is not a valid token (it would be an error for this
 * to appear in any Python program) and does not appear in the grammar, this
 * declaration has no effect on the language recognized.  However, it
 * still provides an easy way to find the external name of an operator (just
 * strip off the leading "@").  (We can't just use "dict_display", since that
 * would tell Horn to define 'dict_display' as a token, whereas it would be a
 * simple identifier if it actually appeared in a Python program.  This is
 * where the kludginess comes in.) The AST_Tree::external_name method does
 * this conversion, converting tokens that are supposed to represent
 * node operators to printable strings.
 */

%token ID

%token PRINTLN "@println"
%token EMPTY 
%token EXPR_LIST "@expr_list"
%token MODULE "@module"
%token BINOP "@binop"
%token UNOP "@unop"
%token INT_LITERAL "@int_literal"
%token DICT_DISPLAY "@dict_display"
%token LIST_DISPLAY "@list_display"
%token PAIR "@pair"
%token COMPARE "@compare"
%token LEFT_COMPARE "@left_compare"
%token ASSIGN "@assign"
%token IF_EXPR "@if_expr"
%token TUPLE "@tuple"
%token DIVDIV "//"
%token POW "**"
%token ATTRIBUTEREF "@attributeref"
%token SUBSCRIPTION "@subscription"
%token SLICING "@slicing"
%token STATEMENT_LIST "@stmt_list"

%token STRING
%token STRING_LITERAL   "@string_literal"

%token INDENT
%token DEDENT
%token NEWLINE

/* Keywords */
%token AND "and"
%token BREAK "break"
%token CLASS "class"
%token CONTINUE "continue"
%token DEF "def"
%token ELIF "elif"
%token ELSE "else"
%token FOR "for"
%token FROM "from"
%token IF "if"
%token IN "in"
%token IS "is"
%token NOT "not"
%token OR "or"
%token PASS "pass"
%token PRINT "print"
%token RETURN "return"
%token WHILE "while"

%%

/************** LEXICAL RULES ********************/
/*
 * All Caps for clarity
 * No unicode, long ints, imaginary, float, or unallowed keywords
 */

/*
 * Predefined: _ANY, _UPPER, _LOWER, _CONTROL, _EOL, _BOL
 */

_BLANK : ' ' | '\t' ;
_NEWLINE : '\n' ;
NEWLINE : _NEWLINE ;

_DIGIT : '0' .. '9' ;
_NONZERODIGIT : '1' .. '9' ;
_HEXDIGIT : _DIGIT | 'a' .. 'f' | 'A' .. 'F' ;
_OCTDIGIT : '0' .. '7' ;

_DECIMALINTEGER : _NONZERODIGIT _DIGIT* | "0" ;
_OCTINTEGER : "0" _OCTDIGIT+ ;
_HEXINTEGER: "0" ( "x" | "X" ) _HEXDIGIT+ ;
_INTEGER : _DECIMALINTEGER | _OCTINTEGER | _HEXINTEGER ;
_EXPONENT : ( "e" | "E" ) ( "+" | "-")? _DIGIT+ ;

_LOWER : 'a' .. 'z' ;
_UPPER : 'A' .. 'Z' ;
_LETTER : _UPPER | _LOWER ;

_RAWSTRINGPREFIX : 'r' | 'R' ;
_RAWESCAPESEQ : '\\' _ANY ;
_ESCAPESEQ : _RAWESCAPESEQ | ( '\\' _OCTDIGIT _OCTDIGIT? _OCTDIGIT? ) ;

/* Careful. Horn rules can only do set subtraction on its own lexical rule */
_NO_SINGLEQUOTE : _ANY - ('\'' | '\n' ) ;
_NO_DOUBLEQUOTE : _ANY - ('"' | '\n') ;
_NO_SLASH : _ANY - ('\\' | '\n') ;

_SHORTSTRINGITEM1 : _NO_SINGLEQUOTE | _ESCAPESEQ ;
_SHORTSTRINGITEM2 : _NO_DOUBLEQUOTE | _ESCAPESEQ ;
_LONGSTRINGITEM : _NO_SLASH | _ESCAPESEQ ;

_RAWSHORTSTRINGITEM1 : _NO_SINGLEQUOTE | _RAWESCAPESEQ ;
_RAWSHORTSTRINGITEM2 : _NO_DOUBLEQUOTE | _RAWESCAPESEQ ;
_RAWLONGSTRINGITEM : _NO_SLASH | _RAWESCAPESEQ ;

_SHORTSTRING : '\'' _SHORTSTRINGITEM1* '\''
    | '"' _SHORTSTRINGITEM2* '"'
    ;
_RAWSHORTSTRING : '\'' _RAWSHORTSTRINGITEM1* '\''
    | '"' _RAWSHORTSTRINGITEM2* '"'
    ;

/* TODO: Helper function find 'shortest sequence of longitems' */
_LONGSTRING : "'''" ( _LONGSTRINGITEM* ("'" | "''")? )* "'''"
    | "\"\"\"" ( _LONGSTRINGITEM* ("\"" | "\"\"")? )* "\"\"\""
    ;
_RAWLONGSTRING : "'''" ( _RAWLONGSTRINGITEM* ("'" | "''")? )* "'''"
    | "\"\"\"" ( _RAWLONGSTRINGITEM* ("\"" | "\"\"")? )* "\"\"\""
    ;

_RAWSTRING : _RAWSTRINGPREFIX (_RAWSHORTSTRING | _RAWLONGSTRING) ;
RAWSTRING : _RAWSTRING ;
_STRINGLITERALPIECE : ( _SHORTSTRING | _LONGSTRING )
    | _RAWSTRING
    ;

/* TODO: fix */
/* Unsupported Keywords. Error */
/* *: ( "as" | "assert" | "del" | "except" | "exec" | "finally" | "future" | "global"
 *   | "import" | "lambda" | "raise" | "try" | "with" | "yield" ) ;
 */

_NO_NEWLINE : _ANY - '\n' ;
_COMMENT : '#' _NO_NEWLINE* ;

/* HANDLE INDENTATION */
* : (' ' | '\t' | '\r')+ ;
* : '\\' _NEWLINE ;
* : _BOL _BLANK* _NEWLINE %prefer             { YYSET_TOKEN(indent($TEXT)); }
    | _BOL _BLANK* _COMMENT _NEWLINE %prefer  { YYSET_TOKEN(indent($TEXT)); }
    | _BOL _BLANK* %prefer                    { YYSET_TOKEN(indent($TEXT)); }
    ;

/* Exclude keywords. */
IDENTIFIER : ( _LETTER | "_" ) ( _LETTER | _DIGIT | "_" )*
    {
      check_identifier($TEXT);
    }
    ;

/* RULES FOR AST */
INT_LITERAL : _INTEGER ;
STRING_LITERAL : _STRINGLITERALPIECE+ ;


/**************** GRAMMAR RULES ********************/

/*
 * Start program
*/
program : stmt_list   { theTree = $^(MODULE, $*); $$ = NULL; }
        | /* empty */ { theTree = $^(MODULE); $$ = NULL; }
        ;

/*
 * Main statements and expressions
 * TODO: Figure out why we can't use NEWLINE instead of '\n'
*/
stmt_list : stmt ('\n'! stmt)* '\n'!
          ;

/*
expression_list : expression (","! expression)+ ","! { $$ = $^(EXPR_LIST, $*); }
                | expression ","!                    { $$ = $^(EXPR_LIST, $*); } 
                ;
*/

/* Introduces a s/r conflict. GLR should be able to solve this one. */
expression_list : expression_list_no_dangling ","! 
                | expression_list_no_dangling
                ;

expression_list_no_dangling : expression (","! expression)*
            { $$ = $^(EXPR_LIST, $*); }
                            ;

expression : conditional_expr 
           ;

enclosure : parenth_form
          | list_display
          | dict_display
          ;

parenth_form : "("! tuple ")"!          
             ;

tuple : expression 
      | expression (","! expression)+ ","!?         { $$ = $^(TUPLE, $*); }
      | expression ","!                             { $$ = $^(TUPLE, $*); }
      ;

/*
 * Basic units
*/
identifier : IDENTIFIER     { $$ = make_id($*); }
           ;
atom : identifier
     | literal
     | enclosure
     ;

primary : atom                       
        | attributeref
        | subscription
        | slicing
        ;

// TODO: String literal node
literal : STRING_LITERAL
        | INT_LITERAL
        ;

//-----------------------------------------------------------------------------
//  Lists
//-----------------------------------------------------------------------------
list_display : "["! expression (","! expression)* ","!? "]"!
                    { $$ = $^(LIST_DISPLAY, $*); }
             | "["! "]"!
                    { $$ = $^(LIST_DISPLAY); }
             ;
//-----------------------------------------------------------------------------
//  Dictionaries
//-----------------------------------------------------------------------------
dict_display : "{"! key_datum_list "}"!             
                    { $$ = $^(DICT_DISPLAY, $*); }
             | "{"! "}"! 
                    { $$ = $^(DICT_DISPLAY); }
             ;

key_datum_list : key_datum (","! key_datum)* ","!?
               ;

key_datum : expression ":"! expression       { $$ = $^(PAIR, $*); }
          ;

attributeref : primary "."! identifier       { $$ = $^(ATTRIBUTEREF, $*); }
             ;

subscription : primary "["! expression_list "]"! { $$ = $^(SUBSCRIPTION, $*); }
             ;

//-----------------------------------------------------------------------------
//  Slicing
//-----------------------------------------------------------------------------
slicing : simple_slicing
        ;

simple_slicing : primary "["! short_slice "]"!  
                    { $$ = $^(SLICING, $*); }
               ;

short_slice : lower_bound ":"! upper_bound  
            ;

lower_bound : expression
            | /* empty */      { $$ = $^(EMPTY); } 
            ;

upper_bound : lower_bound
            ;

/*
 * Primitive operators + - * ** / //
*/
power : primary                     
      | primary "**" u_expr         { $$ = make_binop($*); }
      ;

u_expr : power                     
       | "-" u_expr                 { $$ = make_unop($*); }
       | "+" u_expr                 { $$ = make_unop($*); }
       ;

m_expr : u_expr                    
       | m_expr "*" u_expr          { $$ = make_binop($*); }
       | m_expr "//" u_expr         { $$ = make_binop($*); }
       | m_expr "/" u_expr          { $$ = make_binop($*); }
       | m_expr "%" u_expr          { $$ = make_binop($*); }
       ;

a_expr : m_expr                 
       | a_expr "+" m_expr          { $$ = make_binop($*); }
       | a_expr "-" m_expr          { $$ = make_binop($*); }
       ;

/*
 * Comparisons. Need to differentiate between compare and left_compare
*/
comparison_expr : a_expr comp_operator a_expr                   { $$ = make_compare($*); }
                | a_expr comp_operator comparison_expr          { $$ = make_left_compare($*); }
                ;

comp_operator : "<"
              | ">"
              | "=="
              | ">="
              | "<="
              | "!="
              | "is"
// TODO: Not pass in a constant isnot and notin. 
              | L="is not"              { $$ = make_id("isnot", $L.loc()); } 
              | "in"
              | L="not in"              { $$ = make_id("notin", $L.loc()); }
              ;

/*
 * Boolean operators
*/
conditional_expr : or_test
                 | or_test "if"! or_test "else"! expression 
                        { $$ = make_if_expr($*); }
                 ;

or_test : and_test          
        | or_test "or"! and_test         { $$ = make_or($*); }
        ;
and_test : not_test
         | and_test "and"! not_test      { $$ = make_and($*); }
         ;
not_test : comparison_expr
         | a_expr
         | "not" not_test               { $$ = make_not($*); }
         ;


//-----------------------------------------------------------------------------
//  Statements
//-----------------------------------------------------------------------------
stmt : simple_stmt (";"! simple_stmt)* ";"!?
     | compound_stmt
     ;


//-----------------------------------------------------------------------------
//  Simple Statements
//-----------------------------------------------------------------------------

simple_stmt : expression_stmt
            | assignment_stmt
            | pass_stmt
            | print_stmt
            | return_stmt
            | break_stmt
            | continue_stmt
            ;

expression_stmt : expression_list
                ;

assignment_stmt : target_list "="! expression_list { $$ = make_assign($*); } 
                ;

target_list : "("! main_target_list ")"!
            | "["! main_target_list "]"!
            | main_target_list
            ;

// Needed to resolve some conflicts with target lists
// Is this still needed?
main_target_list : target (","! target)* ","!? 
                 ;

target : identifier                 /* RR conflict with atom. Resolve with GLR. */
       | attributeref               /* RR conflict with primary. Resolve with GLR. */
       | subscription               /* RR conflict with primary. Resolve with GLR. */
       | slicing                    /* RR conflict with primary. Resolve with GLR. */
       ;

pass_stmt : "pass"
          ;

// TODO: Support >>
print_stmt : "print" expression_list_no_dangling ","!
                      { $$ = $^(PRINTLN, $^(EMPTY), $expression_list_no_dangling); }
           | "print" expression_list_no_dangling
                      { $$ = $^(PRINT, $^(EMPTY), $expression_list_no_dangling); }
           ;

// TODO: Check if return is valid (i.e. in funcdef)
return_stmt : "return"! tuple                { validate_return();
                                               $$ = $^(RETURN, $*); 
                                             }
            | "return"!                      { validate_return();
                                               $$ = $^(RETURN, $^(EMPTY));
                                               $$.set_loc(yysource_location());
                                             }
            ;

// TODO: Check if break is valid (i.e. in loop)
break_stmt : "break"^           { validate_break(); } 
           ;

// TODO: Check if continue is valid (i.e. in loop)
continue_stmt : "continue"^     { validate_continue(); }
              ;

//-----------------------------------------------------------------------------
//  Compound Statements
//-----------------------------------------------------------------------------
compound_stmt : if_stmt
              | while_stmt
              | for_stmt
              | funcdef
              | classdef
              ;

if_stmt :  "if"! expression ":"! 
            { ++if_counter; }
            suite elseif_stmt
            { --if_counter;
              $$ = $^(IF, $*); 
            }
        ;

elseif_stmt : else_stmt
            | "\n"! "elif"! expression ":"! suite elseif_stmt 
                    { $$ = $^(IF, $*); }
            ;

else_stmt : "else"! ":"! suite
          | /* empty */         { $$ = $^(STATEMENT_LIST); }
          ;

while_stmt : "while" expression ":" 
             { ++loop_counter; }
             suite else_stmt
             { --loop_counter;
               $$ = $^(WHILE, $*);
             }
           ;

for_stmt : "for" target_list "in" expression_list ":" 
           { ++loop_counter; }
           suite else_stmt
           { --loop_counter;
             $$ = $^(WHILE, $*); 
           }
         ;

// TODO: Change \n to NEWLINE
suite : simple_stmt (";"! simple_stmt)* ";"!?   { $$ = $^(STATEMENT_LIST,
                                                          $*); 
                                                }
      | "\n"! INDENT! statement DEDENT! 
            { $$ = $^(STATEMENT_LIST, $*); }
      ;

statement : stmt_list 
          | stmt_list NEWLINE! statement
          | compound_stmt
          ;

//-----------------------------------------------------------------------------
//  Function and class definitions
//-----------------------------------------------------------------------------

funcdef : "def" funcname "(" parameter_list ")" ":" 
            { ++func_counter; }
          funcblock
            { --func_counter; }
        ;

funcname : identifier
         ;

parameter_list : /* empty */
               | identifier parameter_list
               ;
funcblock : INDENT! stmt_list DEDENT!       
          ; 

// TODO: Should not end with a suite. Define classsuite.
classdef : "class" classname ":"!
            { validate_class(); inside_class = true; }
            suite
            { inside_class = false; }
         ;

classname : identifier
          ;

%%

bool debugParser;

/* Utilities */
//-----------------------------------------------------------------------------
//  Generalizable make functions
//-----------------------------------------------------------------------------

static AST_Ptr make_expr_id_expr(const List_Ptr& operands, int op) {
    return YYMAKE_TREE(op, 
                       operands->at(0), 
                       make_id(operands->at(1)),
                       operands->at(2));
}

static AST_Ptr make_expr_expr(const List_Ptr& operands, int op) {
    return YYMAKE_TREE(op, operands->at(0), operands->at(1));
}


//-----------------------------------------------------------------------------
//  Specific make functions that may use those above internally.
//  At least one of these per entry in the AST table in proj1 handout
//-----------------------------------------------------------------------------
static AST_Ptr make_unop (const List_Ptr& operands)
{
    return YYMAKE_TREE(UNOP, make_id(operands->at(0)), operands->at(1));
}

static AST_Ptr make_binop (const List_Ptr& operands)
{
    return make_expr_id_expr(operands, BINOP);
}

static AST_Ptr make_left_compare(const List_Ptr &operands) {
    return make_expr_id_expr(operands, LEFT_COMPARE);
}

static AST_Ptr make_compare(const List_Ptr &operands) {
    return make_expr_id_expr(operands, COMPARE);
}

static AST_Ptr make_id (const List_Ptr& operands) {
    return make_id(operands->at(0));
}
static AST_Ptr make_id (AST_Ptr& token)
{
    AST_Ptr result = AST::make_token (ID, token->text_size(), 
                                      token->as_chars(), false);
    result->set_loc (token->loc ());
    return result;
}

static AST_Ptr make_id (const char *text, const char *loc) {
    AST_Ptr result = AST::make_token(ID, strlen(text), text, false);
    result->set_loc(loc);
    return result;
}

static AST_Ptr make_if_expr(const List_Ptr &operands) {
    return YYMAKE_TREE(IF_EXPR, operands->at(0),
                       operands->at(1), operands->at(2));
}

static AST_Ptr make_and(const List_Ptr &operands) {
    return make_expr_expr(operands, AND);
}

static AST_Ptr make_or(const List_Ptr &operands) {
    return make_expr_expr(operands, OR);
}

static AST_Ptr make_not(const List_Ptr &operands) {
    return make_unop(operands);
}

static AST_Ptr make_assign(const List_Ptr &operands) {
    return YYMAKE_TREE(ASSIGN, operands->at(0), operands->at(1));
}

int
AST::lineNumber ()
{
    return yylocation_line (loc ());
}

void
parse (FILE* f, const string& name)
{
    if (debugParser)
        yydebug = 1;
    yypush_lexer (f, name);
    theTree = NULL;
    yyparse ();
    if (theTree != NULL) {
        theTree->print (cout, 0);
    }
}

//-----------------------------------------------------------------------------
//  Validation functions
//-----------------------------------------------------------------------------
static void validate_class() {
    if (inside_class || func_counter > 0 || if_counter > 0) {
        error(yysource_location(), "Nested class definition not allowed!");
    }
}

static void validate_return() {
    if (func_counter == 0) {
        error(yysource_location(), 
              "Return statements must be inside functions!");
    }
}

static void validate_break() {
    if (loop_counter == 0) {
        error(yysource_location(),
              "Break statements must be inside loops!");

    }
}

static void validate_continue() {
    if (loop_counter == 0) {
        error(yysource_location(),
              "Continue statements must be inside loops!");
    }
}

/* -------------- HELPER FUNCTIONS ------------- */
static void
check_identifier (const char* text)
{
  for (int i = 0; i < 15; i++ )
  {
    if ( invalid_keywords[i].compare(text) == 0 ) {
      error(yysource_location(), "Identifier is illegall keyword");
    }
  }
  for (int i = 0; i < 18; i++ )
  {
    if ( keywords[i].compare(text) == 0 ) {
      error(yysource_location(), "Identifier is a keyword");
    }
  }
}

static int
indent_size (const char* indent)
{
  int size = 0;
  for (int i = 0; indent[i] != '\0'; i++)
  {
    if (indent[i] == ' ')
    {
      size += 1;
    }
    else if (indent[i] == '\t')
    {
      size += 8 - (size % 8);
    }
    else
    {
      break;
    }
  }
  return size;
}

static int
indent (const char* indent)
{
  int indent_length = indent_size(indent);

  // indent
  if (indent_length > current_indent_length)
  {
    indent_stack.push(indent_length - current_indent_length);
    current_indent_length = indent_length;
    return INDENT;
  }

  // dedent
  else if (indent_length < current_indent_length)
  {
    while (!indent_stack.empty() && indent_length < current_indent_length)
    {
      current_indent_length -= indent_stack.top();
      indent_stack.pop();

      if (indent_length == current_indent_length) 
      {
        yyqueue_token('\n', 0, 0, "", 0);
        return DEDENT;
      }
      yyqueue_token('\n', 0, 0, "", 0);
      yyqueue_token(DEDENT, 0, 0, "", 0);
    }
    error(yysource_location(), "Indentation error");
    return -1;
  }

  // same level of indentation
  else
  {
    return -1;
  }
}

